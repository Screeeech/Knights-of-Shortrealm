extends Node2D

@onready var label: Label = $InteractLabel
@onready var player: Player = get_tree().get_first_node_in_group("player") 

var active_areas: Array[InteractionArea] = []
var can_interact: bool = true

func register_area(area: InteractionArea):
    active_areas.push_back(area)

func deregister_area(area: InteractionArea):
    var index: int = active_areas.find(area)
    if index != -1:
        active_areas.remove_at(index)

func _process(_delta: float) -> void:
    if active_areas.size() > 0 and can_interact:
        active_areas.sort_custom(_sort_by_distance_to_player)
        label.text = active_areas[0].interact_text
        label.global_position = active_areas[0].global_position
        label.global_position += active_areas[0].label_offset
        label.global_position.x -= label.size.x / 2
        label.show()
    else:
        label.hide()

func _input(event: InputEvent) -> void:
    if active_areas.size() <= 0:
        return

    var interaction_name: String = get_interaction_name(event)
    if interaction_name != "" and can_interact:
        can_interact = false
        label.hide()

        await active_areas[0].interact.call(interaction_name)

        can_interact = true

func _sort_by_distance_to_player(area1: InteractionArea, area2: InteractionArea) -> bool:
    var distance1: float = player.global_position.distance_to(area1.global_position)
    var distance2: float = player.global_position.distance_to(area2.global_position)
    
    return distance1 < distance2

func get_interaction_name(event: InputEvent) -> String:
    for i in active_areas[0].interact_actions.size():
        if event.is_action_pressed(active_areas[0].interact_actions[i]):
            return active_areas[0].interact_actions[i]

    return ""
